apply plugin: "com.android.application"
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: "androidx.navigation.safeargs"

android {
    compileSdkVersion 30


    defaultConfig {
        applicationId "com.ramitsuri.expensemanager.re"
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 49
        versionName "6.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }

        sourceSets {
            androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
        }

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"

    implementation "androidx.constraintlayout:constraintlayout:2.0.3"
    implementation "androidx.coordinatorlayout:coordinatorlayout:1.1.0"

    // Fragments
    implementation "androidx.fragment:fragment:1.3.0-beta01"
    implementation "androidx.preference:preference:1.1.1"

    // Navigation
    implementation "androidx.navigation:navigation-fragment:2.3.1"
    implementation "androidx.navigation:navigation-ui:2.3.1"

    // Material Components
    implementation "com.google.android.material:material:1.3.0-alpha03"

    // Room (Use 2.1.0 to satisfy bug with kotlin stdlib)
    implementation "androidx.room:room-common:2.2.5"
    implementation "androidx.room:room-runtime:2.2.5"
    androidTestImplementation "androidx.room:room-testing:2.2.5"
    annotationProcessor "androidx.room:room-compiler:2.2.5"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"

    // Firebase
    implementation "com.google.firebase:firebase-core:18.0.0"
    implementation 'com.google.firebase:firebase-crashlytics:17.2.2'
    implementation 'com.google.firebase:firebase-analytics:18.0.0'
    implementation 'com.google.firebase:firebase-firestore:22.0.0'

    // Logging
    implementation "com.jakewharton.timber:timber:4.7.1"

    // Google Auth
    implementation "com.google.android.gms:play-services-auth:17.0.0"

    // Work
    implementation "androidx.work:work-runtime:2.5.0-alpha03"
    implementation "androidx.work:work-testing:2.5.0-alpha03"

    // Google Sheets and Drive
    implementation("com.google.api-client:google-api-client-android:1.22.0") {
        exclude group: "org.apache.httpcomponents"
    }
    implementation("com.google.apis:google-api-services-sheets:v4-rev40-1.22.0") {
        exclude group: "org.apache.httpcomponents"
    }
    implementation("com.google.apis:google-api-services-drive:v3-rev110-1.23.0") {
        exclude group: "org.apache.httpcomponents"
    }

    // Gson
    implementation 'com.google.code.gson:gson:2.8.6'

    // Test
    androidTestImplementation "androidx.test:core:1.3.1-alpha02"
    androidTestImplementation "androidx.test:runner:1.3.1-alpha02"
    //androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0-alpha02"
    androidTestImplementation "android.arch.core:core-testing:1.1.1"
    testImplementation "junit:junit:4.13.1"
    testImplementation 'androidx.test:core:1.3.0'
    testImplementation 'org.mockito:mockito-core:3.3.3'

    // SheetsCore
    implementation("com.ramitsuri:sheetscore:3.1.1")

    // Due to error - Duplicate class com.google.common.util.concurrent.ListenableFuture found in
    // modules guava-jdk5-17.0.jar (com.google.guava:guava-jdk5:17.0) and listenablefuture-1.0.jar
    // (com.google.guava:listenablefuture:1.0)
    configurations {
        all*.exclude group: "com.google.guava", module: "listenablefuture"
    }
    configurations {
        all*.exclude group: "com.google.guava", module: "guava-jdk5"
    }

    // Use for - Cannot find a version of 'com.google.code.findbugs:jsr305' that satisfies the version constraints
    androidTestImplementation 'com.google.code.findbugs:jsr305:1.3.9'

}
apply plugin: "com.google.gms.google-services"
