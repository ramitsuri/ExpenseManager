apply plugin: "com.android.application"
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: "androidx.navigation.safeargs"
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 30


    defaultConfig {
        applicationId "com.ramitsuri.expensemanager.re"
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 53
        versionName "6.2.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }

        sourceSets {
            androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
        }

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.returnDefaultValues = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    implementation "androidx.appcompat:appcompat:$app_compat_version"
    implementation "androidx.legacy:legacy-support-v4:$legacy_support_version"

    implementation "androidx.constraintlayout:constraintlayout:$constraint_layout_version"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$coordinator_layout_version"

    // Fragments
    implementation "androidx.fragment:fragment:$fragment_version"
    implementation "androidx.preference:preference:$preference_version"

    // Navigation
    implementation "androidx.navigation:navigation-fragment:$navigation_version"
    implementation "androidx.navigation:navigation-ui:$navigation_version"

    // Material Components
    implementation "com.google.android.material:material:$material_version"

    // Room (Use 2.1.0 to satisfy bug with kotlin stdlib)
    implementation "androidx.room:room-common:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    androidTestImplementation "androidx.room:room-testing:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    // Firebase
    implementation "com.google.firebase:firebase-core:$firebase_core_version"
    implementation "com.google.firebase:firebase-crashlytics:$crashlytics_version"
    implementation "com.google.firebase:firebase-analytics:$analytics_version"
    implementation "com.google.firebase:firebase-firestore:$firestore_version"

    // Logging
    implementation "com.jakewharton.timber:timber:$timber_version"

    // Google Auth
    implementation "com.google.android.gms:play-services-auth:$play_service_auth_version"

    // Work
    implementation "androidx.work:work-runtime:$work_version"
    implementation "androidx.work:work-testing:$work_version"

    // Google Sheets and Drive
    implementation("com.google.api-client:google-api-client-android:$google_client_version") {
        exclude group: "org.apache.httpcomponents"
    }
    implementation("com.google.apis:google-api-services-sheets:$google_sheets_version") {
        exclude group: "org.apache.httpcomponents"
    }
    implementation("com.google.apis:google-api-services-drive:$google_drive_version") {
        exclude group: "org.apache.httpcomponents"
    }

    // Gson
    implementation "com.google.code.gson:gson:$gson_version"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Coroutine
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"

    // Test
    androidTestImplementation "androidx.test:core:$test_core_version"
    androidTestImplementation "androidx.test:runner:$test_core_version"
    androidTestImplementation "android.arch.core:core-testing:$test_arch_core_version"
    androidTestImplementation "androidx.test.ext:junit:$test_ext_junit"
    testImplementation "junit:junit:$test_junit_version"
    testImplementation "androidx.test:core:$test_core_version"
    testImplementation "org.mockito:mockito-core:$test_mockito_core_version"

    // SheetsCore
    implementation "com.ramitsuri:sheetscore:$sheets_core_version"

    // Due to error - Duplicate class com.google.common.util.concurrent.ListenableFuture found in
    // modules guava-jdk5-17.0.jar (com.google.guava:guava-jdk5:17.0) and listenablefuture-1.0.jar
    // (com.google.guava:listenablefuture:1.0)
    configurations {
        all*.exclude group: "com.google.guava", module: "listenablefuture"
    }
    configurations {
        all*.exclude group: "com.google.guava", module: "guava-jdk5"
    }

    // Use for - Cannot find a version of 'com.google.code.findbugs:jsr305' that satisfies the version constraints
    androidTestImplementation 'com.google.code.findbugs:jsr305:1.3.9'

}
apply plugin: "com.google.gms.google-services"
